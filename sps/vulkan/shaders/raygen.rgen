#version 460
#extension GL_EXT_ray_tracing : require

layout(set = 0, binding = 0) uniform accelerationStructureEXT topLevelAS;
layout(set = 0, binding = 1, rgba8) uniform image2D outputImage;
layout(set = 0, binding = 2) uniform CameraUBO {
  mat4 view;          // Raster: view matrix (not used in RT)
  mat4 proj;          // Raster: proj matrix (not used in RT)
  mat4 viewInverse;   // RT: camera transform
  mat4 projInverse;   // RT: unproject rays
  vec4 lightPosition;
  vec4 lightColor;
  vec4 lightAmbient;
  vec4 viewPos;
  vec4 material;
} ubo;

layout(location = 0) rayPayloadEXT vec3 hitValue;

void main()
{
  const vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
  const vec2 inUV = pixelCenter / vec2(gl_LaunchSizeEXT.xy);
  vec2 d = inUV * 2.0 - 1.0;

  // Transform from clip space to world space
  vec4 origin = ubo.viewInverse * vec4(0, 0, 0, 1);
  vec4 target = ubo.projInverse * vec4(d.x, d.y, 1, 1);
  vec4 direction = ubo.viewInverse * vec4(normalize(target.xyz), 0);

  float tmin = 0.001;
  float tmax = 10000.0;

  hitValue = vec3(0.0);

  traceRayEXT(
    topLevelAS,           // acceleration structure
    gl_RayFlagsOpaqueEXT, // rayFlags
    0xFF,                 // cullMask
    0,                    // sbtRecordOffset
    0,                    // sbtRecordStride
    0,                    // missIndex
    origin.xyz,           // ray origin
    tmin,                 // ray min range
    direction.xyz,        // ray direction
    tmax,                 // ray max range
    0                     // payload location
  );

  imageStore(outputImage, ivec2(gl_LaunchIDEXT.xy), vec4(hitValue, 1.0));
}
